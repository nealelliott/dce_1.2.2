#
# @OSF_COPYRIGHT@
# COPYRIGHT NOTICE
# Copyright (c) 1996 Open Software Foundation, Inc.
# ALL RIGHTS RESERVED (DCE).  See the file named COPYRIGHT.DCE for
# the full copyright text.
#
# HISTORY
# $Log: configure.in,v $
# Revision 1.1.2.1  1996/08/09  17:24:16  arvind
# 	DCE 1.2.2 Intermediate drop (post bl3/pre bl4)
# 	[1996/08/09  14:09:00  arvind]
#
# $EndLog$
#

# Process this file with autoconf to produce a configure script.

define([AC_CACHE_LOAD], )
define([AC_CACHE_SAVE], )

AC_INIT(expect.h)
AC_CONFIG_HEADER(expect_cf.h)

# If `configure' is invoked (in)directly via `make', ensure that it
# encounters no `make' conflicts.
#
unset MFLAGS MAKEFLAGS

AC_PROG_CC
AC_PROG_CPP
CC=${CC-cc}
# use a version with better error trapping till our autoconf patch
# gets accepted. We need this here even if we're only doing native
# builds cause the tests later get the wrong answers if the native
# compiler is hosed and it isn't caught.
# FIXME: until the autoconf macro is fixed, "checking whether
#        cross-compiling" will always be printed twice
#        printed twice.
dnl	 This is because other macros call AC_C_CROSS directly
#	 which ignores the errors
CY_C_CROSS

AC_PROG_RANLIB

# this'll use a BSD compatible install or our included install-sh
AC_PROG_INSTALL

# these are the other subdirectories we need to configure
AC_CONFIG_SUBDIRS(testsuite)

# This is for LynxOS, which needs a flag to force true POSIX when
# building.  The flag varies depending how old the compiler is.
# -X is for the old "cc" and "gcc" (based on 1.42)
# -mposix is for the new gcc (at least 2.5.8)
# This modifies the value of $CC to have the POSIX flag added
# so it'll configure correctly
CY_TCL_LYNX_POSIX

AC_TYPE_PID_T
AC_RETSIGTYPE
dnl AC_TIME_WITH_SYS_TIME
AC_HEADER_TIME
AC_HEADER_SYS_WAIT

# Use -g on all systems but Linux where it upsets the dynamic X libraries.
AC_MSG_CHECKING([if we are running Linux])
if test "x`(uname) 2>/dev/null`" = xLinux; then
  AC_MSG_RESULT(yes)
  linux=1
  EXP_CFLAGS=
else
  AC_MSG_RESULT(no)
  linux=0
  EXP_CFLAGS=-g
fi

# Solaris 2.4 and later requires __EXTENSIONS__ in order to see all sorts
# of traditional but not standard stuff in header files.
# For simplicity, use it on all SunOS machines.  (Solaris uname identifies
# itself as SunOS.)
AC_MSG_CHECKING([if we are running SunOS or Solaris])
if test "x`(uname) 2>/dev/null`" = xSunOS; then
  AC_MSG_RESULT(yes)
  AC_DEFINE(SUNOS)
else
  AC_MSG_RESULT(no)
fi

#
# On a few systems, libm.a is the same as libc.a
#
AC_CHECK_FUNC(sin, , LIBS="${LIBS} -lm" )


#
# Look for X11
#
AC_PATH_X

# this is only needed on really old versions of HP-UX - rob
# can't reproduce requirement for this code on my HP - DEL
#
# HP-UX wants this instead of libX11
#
dnl if test x"$x_libraries" = x ; then
dnl   AC_CHECK_LIB(Xwindow, ?function?, XLIBS="-lXwindow")
dnl fi

#
# Ok, lets find the tcl source trees so we can use the headers
#
CY_PATH_TCL
if test x"$no_tcl" = x"true" ; then
    echo "	ERROR: Can't find Tcl headers or library."
    echo "	See README for information on how to obtain Tcl."
    echo "	If Tcl is installed, see INSTALL on how to tell"
    echo "	configure where Tcl is installed."
    exit 1
fi

# only look for Tk stuff if we have X11
if test x"$no_x" != x"true" ; then
  CY_PATH_TK
  if test x"$no_tk" = x"true" ; then
    echo "	WARNING: Can't find Tk headers or library."
    echo "	You can still build expect, but not expectk."
    echo "	See README for information on how to obtain Tk."
    echo "	If Tk is installed, see INSTALL on how to tell"
    echo "	configure where Tk is installed."
  fi
fi

#
# Now that we've found the Tcl sources, configure the debugger
# this is a little tricky because it has its own configure script
# which produces a Makefile and cf file.  We only want the cf file,
# so switch to a temporary directory and run the debugger's configure.
# Then save the cf file and delete the rest.
#
# Incidentally, the debugger can't depend on Expect's cf file, because
# the debugger is designed to be independent of Expect.
# 

test -n "$verbose" && echo "configuring Tcl debugger"
tmpdir=./Dbg$$
mkdir ${tmpdir}
#cp $TCLHDIR/tclInt.h ${tmpdir}
cp ${srcdir}/Dbgconfigure ${srcdir}/Dbg.h ${srcdir}/Dbg_cf.h.in ${srcdir}/install-sh ${tmpdir}
cp $srcdir/DbgMkfl.in ${tmpdir}/Makefile.in
  (cd $tmpdir; /bin/sh Dbgconfigure --with-tclinclude=`echo ${TCLHDIR} | sed -e 's/-I//'`)
cp ${tmpdir}/Dbg_cf.h .
rm -rf $tmpdir
test -n "$verbose" && echo "configured Tcl debugger"

# Pre-7.4 versions of Tcl do not have Regerror function
AC_MSG_CHECKING([if Tcl uses regerror function])
if test $tclmajor -ge 7 -a $tclminor -ge 4 ; then
	AC_MSG_RESULT(yes)
	AC_DEFINE(HAVE_TCLREGERROR)
else
	AC_MSG_RESULT(no)
fi

unset ac_cv_func_socket
AC_CHECK_FUNC(socket, socket=1 , socket=0)
if test $socket -eq 0 ; then
  # -lsocket is broken on IRIX, so avoid
  # this test is probably unnecessary now
  # but don't have both old and new SGIs around to check
  if test "x`(uname) 2>/dev/null`" != xIRIX; then
    AC_CHECK_LIB(socket, socket, XLIBS="$XLIBS -lsocket")
  fi
fi

# required by ISC 4.0 using XFree86
unset ac_cv_func_gettimeofday
AC_CHECK_FUNC(gettimeofday, gettimeofday=1 , gettimeofday=0)
if test $gettimeofday -eq 0 ; then
  AC_CHECK_LIB(rpc, gettimeofday, XLIBS="$XLIBS -lrpc")
fi

unset ac_cv_func_gethostname
AC_CHECK_FUNC(gethostname, gethostname=1 , gethostname=0)
if test $gethostname -eq 0 ; then
  AC_CHECK_LIB(nsl, gethostname, XLIBS="$XLIBS -lnsl")
fi

# required by Sequent ptx2
unset ac_cv_func_t_open
AC_CHECK_FUNC(t_open, t_open=1 , t_open=0)
if test $t_open -eq 0 ; then
  AC_CHECK_LIB(nsl, t_open, XLIBS="$XLIBS -lnsl")
fi

# required by ISC 4.0 using native X11
unset ac_cv_func_t_open
AC_CHECK_FUNC(t_open, t_open=1 , t_open=0)
if test $t_open -eq 0 ; then
  AC_CHECK_LIB(nsl_s, t_open, XLIBS="$XLIBS -lnsl_s")
fi

# required by Sequent ptx2
unset ac_cv_func_gethostname
AC_CHECK_FUNC(gethostname, gethostname=1 , gethostname=0)
if test $gethostname -eq 0 ; then
  AC_CHECK_LIB(inet, gethostname, XLIBS="$XLIBS -linet" LIBS="$LIBS -linet")
fi

# required by Fischman's ISC 4.0
unset ac_cv_func_socket
AC_CHECK_FUNC(socket, socket=1 , socket=0)
if test $socket -eq 0 ; then
  AC_CHECK_LIB(inet, socket, XLIBS="$XLIBS -linet" LIBS="$LIBS -linet")
fi

unset ac_cv_func_select
AC_CHECK_FUNC(select, select=1 , select=0)
if test $select -eq 0 ; then
  AC_CHECK_LIB(inet, select, XLIBS="$XLIBS -linet" LIBS="$LIBS -linet")
fi

unset ac_cv_func_getpseudotty
AC_CHECK_FUNC(getpseudotty, getpseudotty=1 , getpseudotty=0)
if test $getpseudotty -eq 0 ; then
  AC_CHECK_LIB(seq, getpseudotty)
fi

#
# Now let's try the same with Sequent DYNIX/ptx version 2
# (all this stuff should be done automatically by feature testing elsewhere)
# (hence, it is tentatively commented out)

dnl AC_MSG_CHECKING([if we are on a Sequent])
dnl if test x"`(/etc/showcfg | egrep 'SCED|SSM|CSM')2>/dev/null`" != x; then
dnl    AC_MSG_RESULT(yes)
dnl    # We should be on a Sequent machine
dnl    if test "uname -s" = DYNIX/ptx; then
dnl       # We're on a 4.0 or greater release
dnl       AC_CHECK_LIB(socket, socket, [XLIBS="$XLIBS -lsocket"])
dnl       # The above might get resolved by shared lib and won't therefore
dnl       # truly be standalone
dnl    else
dnl       # We're assuming this is ptx and not Dynix 3
dnl       AC_CHECK_LIB(inet, select, XLIBS="$XLIBS -linet" LIBS="$LIBS -lseq")
dnl       AC_CHECK_LIB(seq, getpseudotty)
dnl    fi
dnl else
dnl    AC_MSG_RESULT(no)
dnl fi

#
# Look for various header files
#
AC_CHECK_HEADER(sys/sysmacros.h, AC_DEFINE(HAVE_SYSMACROS_H))
AC_CHECK_HEADER(stdlib.h, ,AC_DEFINE(NO_STDLIB_H))
AC_CHECK_HEADER(stdarg.h, AC_DEFINE(HAVE_STDARG_H))
AC_CHECK_HEADER(varargs.h, AC_DEFINE(HAVE_VARARGS_H))
AC_CHECK_HEADER(unistd.h, AC_DEFINE(HAVE_UNISTD_H))
AC_CHECK_HEADER(sys/stropts.h, AC_DEFINE(HAVE_STROPTS_H))
AC_CHECK_HEADER(sys/sysconfig.h, AC_DEFINE(HAVE_SYSCONF_H))
AC_CHECK_HEADER(sys/fcntl.h, AC_DEFINE(HAVE_SYS_FCNTL_H))
AC_CHECK_HEADER(sys/select.h, AC_DEFINE(HAVE_SYS_SELECT_H))
AC_CHECK_HEADER(sys/time.h, AC_DEFINE(HAVE_SYS_TIME_H))
AC_CHECK_HEADER(sys/ptem.h, AC_DEFINE(HAVE_SYS_PTEM_H))
AC_CHECK_HEADER(sys/strredir.h, AC_DEFINE(HAVE_STRREDIR_H))
AC_CHECK_HEADER(sys/strpty.h, AC_DEFINE(HAVE_STRPTY_H))

dnl #echo checking for ucbinclude/sys/ioctl.h (ucb-style ioctl.h under SV)
dnl #if test -f /usr/ucbinclude/sys/ioctl.h ; then
dnl #    AC_DEFINE(HAVE_UCB_IOCTL_H)
dnl #fi

AC_MSG_CHECKING([for sys/bsdtypes.h])
if test "ISC_${ISC}" = "ISC_1"   ;    then
   AC_MSG_RESULT(yes)
   # if on ISC 1, we need <sys/bsdtypes.h> to get FD_SET macros
   AC_HAVE_HEADERS(sys/bsdtypes.h)
else
   AC_MSG_RESULT(no)
fi

#
# Look for functions that may be missing
#
dnl AC_CHECK_FUNC(memcpy, AC_DEFINE(HAVE_MEMCPY))
AC_CHECK_FUNC(memmove, AC_DEFINE(HAVE_MEMMOVE))
AC_CHECK_FUNC(sysconf, AC_DEFINE(HAVE_SYSCONF))
AC_CHECK_FUNC(strftime, AC_DEFINE(HAVE_STRFTIME))
AC_CHECK_FUNC(strchr, AC_DEFINE(HAVE_STRCHR))
AC_CHECK_FUNC(timezone, AC_DEFINE(HAVE_TIMEZONE))

# dnl check for memcpy by hand
# because Unixware 2.0 handles it specially and refuses to compile
# autoconf's automatic test that is a call with no arguments
AC_MSG_CHECKING([for memcpy])
AC_TRY_LINK(,[
char *s1, *s2;
memcpy(s1,s2,0);
],
	AC_MSG_RESULT(yes)
	AC_DEFINE(HAVE_MEMCPY)
	,
	AC_MSG_RESULT(no)
)



dnl AC_MSG_CHECKING([if wait expects union])
dnl AC_TRY_COMPILE([#include <sys/types.h> 
dnl #include <sys/wait.h>], [
dnl pid_t pid;
dnl union wait x;
dnl WIFEXITED(x);		/* Generates compiler error if WIFEXITED
dnl 			 * requires an int. */
dnl #ifdef HAVE_WAITPID
dnl 	/* On some systems, WIFEXITED accepts a union and casts it to an int */
dnl 	/* but waitpid's prototype insists on an int, so check that too. */
dnl 	waitpid(pid,&x,0);
dnl #endif
dnl ],
dnl 	AC_MSG_RESULT(yes)
dnl ,
dnl 	AC_DEFINE(NO_UNION_WAIT)
dnl 	AC_MSG_RESULT(no)
dnl )

# Some systems only define WNOHANG if _POSIX_SOURCE is defined
# The following merely tests that sys/wait.h can be included
# and if so that WNOHANG is not defined.  The only place I've
# seen this is ISC.
AC_MSG_CHECKING([if WNOHANG requires _POSIX_SOURCE])
AC_TRY_RUN([
#include <sys/wait.h>
main() {
#ifndef WNOHANG
	return 0;
#else
	return 1;
#endif
}],
	AC_MSG_RESULT(yes)
	AC_DEFINE(WNOHANG_REQUIRES_POSIX_SOURCE)
,
	AC_MSG_RESULT(no)
,
	AC_MSG_ERROR([Expect can't be cross compiled])
)

AC_MSG_CHECKING([if any value exists for WNOHANG])
rm -rf wnohang
AC_TRY_RUN([
#include <stdio.h>
#include <sys/wait.h>
main() {
#ifdef WNOHANG
	FILE *fp = fopen("wnohang","w");
	fprintf(fp,"%d",WNOHANG);
	fclose(fp);
	return 0;
#else
	return 1;
#endif
}],
	AC_MSG_RESULT(yes)
	AC_DEFINE_UNQUOTED(WNOHANG_BACKUP_VALUE, `cat wnohang`)
	rm -f wnohang
,
	AC_MSG_RESULT(no)
	AC_DEFINE(WNOHANG_BACKUP_VALUE, 1)
,
	AC_MSG_ERROR([Expect can't be cross compiled])
)

#
# check how signals work
#

# Check for the data type of the mask used in select().
# This picks up HP braindamage which defines fd_set and then 
# proceeds to ignore it and use int.
# Pattern matching on int could be loosened.
# Can't use ac_header_egrep since that doesn't see prototypes with K&R cpp.
AC_MSG_CHECKING([mask type of select])
if egrep "select\(size_t, int" /usr/include/sys/time.h >/dev/null 2>&1; then
  AC_MSG_RESULT(int)
  AC_DEFINE(SELECT_MASK_TYPE, int)
else
  AC_MSG_RESULT(none)
fi

dnl # Check for the data type of the function used in signal(). This
dnl # must be before the test for rearming.
dnl # echo checking return type of signal handlers
dnl AC_HEADER_EGREP([(void|sighandler_t).*signal], signal.h, retsigtype=void,AC_DEFINE(RETSIGTYPE, int) retsigtype=int)

# FIXME: check if alarm exists
AC_MSG_CHECKING([if signals need to be re-armed])
AC_TRY_RUN([
#include <signal.h>
#define RETSIGTYPE $retsigtype

int signal_rearms = 0;

RETSIGTYPE
child_sigint_handler(n)
int n;
{
}

RETSIGTYPE
parent_sigint_handler(n)
int n;
{
signal_rearms++;
}

main()
{
	signal(SIGINT,parent_sigint_handler);

	if (0 == fork()) {
		signal(SIGINT,child_sigint_handler);
		kill(getpid(),SIGINT);
		kill(getpid(),SIGINT);
		kill(getppid(),SIGINT);
	} else {
		int status;

		wait(&status);
		unlink("core");
		exit(signal_rearms);
	}
}],
	AC_MSG_RESULT(yes)
	AC_DEFINE(REARM_SIG)
,
	AC_MSG_RESULT(no)
, AC_MSG_WARN([Expect can't be cross compiled])
)

# HPUX7 has trouble with the big cat so split it
# Owen Rees <rtor@ansa.co.uk> 29Mar93 
SEDDEFS="${SEDDEFS}CONFEOF
cat >> conftest.sed <<CONFEOF
"
#

# There are multiple versions of getpty, alas.
# I don't remember who has the first one, but Convex just added one
# so check for it.  Unfortunately, there is no header so the only
# reasonable way to make sure is to look it we are on a Convex.
# Gee, there's no uname(1) on a Convex either!

AC_MSG_CHECKING([if we are on a Convex])
# need this for later
AC_TRY_RUN([
#include <sys/utsname.h>
main()
{
  struct utsname namep;
  uname(&namep);
  /* return from strcmp is backwards, but then, so is exit! */
  exit(strcmp("ConvexOS",namep.sysname));
}],
	AC_MSG_RESULT(yes)
	AC_DEFINE(CONVEX)
	convex=1
,
	AC_MSG_RESULT(no)
	convex=0
,
	AC_MSG_ERROR([Expect can't be cross compiled])
)

# both Next and pure Mach behave identically with respect
# to a few things, so just lump them together as "mach"

AC_MSG_CHECKING([if we are running pure Mach])
if test "x`(uname) 2>/dev/null`" = xMach; then
  AC_MSG_RESULT(yes)
  mach=1
else
  AC_MSG_RESULT(no)
  mach=0
fi

AC_MSG_CHECKING([if we are running MachTen])
if test -r /MachTen ; then
  AC_MSG_RESULT(yes)
  mach=1
else
  AC_MSG_RESULT(no)
  mach=0
fi

AC_MSG_CHECKING([if we are on a NeXT])
if test -r /NextApps ; then
  AC_MSG_RESULT(yes)
  mach=1
else
  AC_MSG_RESULT(no)
  mach=0
fi

DEFAULT_STTY_ARGS='"sane"'

if test $mach -eq 1 ; then
	DEFAULT_STTY_ARGS="cooked"
fi

AC_MSG_CHECKING([if stty reads from stdout])
# On Solaris and other SVR4 based systems, this stty
# can't be run in the background, so we trap this. 
# Unfortunately, it gets the wrong answer, but at least
# it doesn't hang configuring. FIXME
# unfortunately, robs@sco.com notes that this fails on SCO OSR5, to wit:
# "it fails because 16 is the highest signal trappable in sh on this platform"
# therefore, I've commented it out - Don
# trap "true" 21 22 26 27
stty_reads_stdout=0
/bin/stty > /dev/null 2> /dev/null
if test $? -ne 0 ; then
    stty_reads_stdout=1
else
    # some machines don't complain - just hardwire them
    if test $mach -eq 1 ; then
        stty_reads_stdout=1
    fi
    # not sure about old convex but 5.2 definitely reads from stdout
    if test $convex -eq 1 ; then
        stty_reads_stdout=1
    fi
    if test -r /usr/apollo/bin ; then
        stty_reads_stdout=1
    fi
    if test -r /bin/pyr ; then
        if /bin/pyr ; then
            stty_reads_stdout=1
        fi
    fi
fi
if test $stty_reads_stdout -eq 1 ; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(STTY_READS_STDOUT)
else
    AC_MSG_RESULT(no)
fi

# Look for various features to determine what kind of pty
# we have. For some weird reason, ac_compile_check would not
# work, but ac_test_program does.
#
AC_MSG_CHECKING([for HP style pty allocation])
# following test fails on DECstations and other things that don't grok -c
# but that's ok, since they don't have PTYMs anyway
if test -r /dev/ptym/ptyp0 2>/dev/null ; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(HAVE_PTYM)
else
    AC_MSG_RESULT(no)
fi

AC_MSG_CHECKING([for HP style pty trapping])
AC_HEADER_EGREP([struct.*request_info], sys/ptyio.h,
	AC_MSG_RESULT(yes)
	AC_DEFINE(HAVE_PTYTRAP)
,
	AC_MSG_RESULT(no)
)

AC_MSG_CHECKING([for AIX new-style pty allocation])
if test -r /dev/ptc -a -r /dev/pts ; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(HAVE_PTC_PTS)
else
    AC_MSG_RESULT(no)
fi

AC_MSG_CHECKING([for SGI old-style pty allocation])
if test -r /dev/ptc -a ! -r /dev/pts ; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(HAVE_PTC)
else
    AC_MSG_RESULT(no)
fi

AC_MSG_CHECKING([for SVR4 style pty allocation])
if test -r /dev/ptmx ; then
  AC_MSG_RESULT(yes)
  AC_DEFINE(HAVE_PTMX)
  # aargg. Some systems need libpt.a to use /dev/ptmx
  AC_CHECK_FUNC(ptsname, , LIBS="${LIBS} -lpt")
else
  AC_MSG_RESULT(no)
fi

# In OSF/1 case, SVR4 are somewhat different.
# Gregory Depp <depp@osf.org> 17Aug93
AC_MSG_CHECKING([for OSF/1 style pty allocation])
if test -r /dev/ptmx_bsd ; then
    AC_DEFINE(HAVE_PTMX_BSD)
    AC_MSG_RESULT(yes)
else
    AC_MSG_RESULT(no)
fi

tcgetattr=0
tcsetattr=0
AC_CHECK_FUNC(tcgetattr, tcgetattr=1)
AC_CHECK_FUNC(tcsetattr, tcsetattr=1)
if test $tcgetattr -eq 1 -a $tcsetattr -eq 1 ; then
    AC_DEFINE(HAVE_TCSETATTR)
    AC_DEFINE(POSIX)
fi

# first check for the pure bsd
AC_MSG_CHECKING([for struct sgttyb])
AC_TRY_RUN([
#include <sgtty.h>
main()
{
  struct sgttyb tmp;
  exit(0);
}],
        AC_MSG_RESULT(yes)
        AC_DEFINE(HAVE_SGTTYB)
        PTY_TYPE=sgttyb
,
        AC_MSG_RESULT(no)
,
	AC_MSG_ERROR([Expect can't be cross compiled])
)

if test $mach -eq 0 ; then

  # next check for the older style ttys
  # note that if we detect termio.h (only), we still set PTY_TYPE=termios
  # since that just controls which of pty_XXXX.c file is use and
  # pty_termios.c is set up to handle pty_termio.
  AC_MSG_CHECKING([for struct termio])
  AC_TRY_RUN([#include <termio.h>
  main()
  {
    struct termio tmp;
    exit(0);
  }],
        AC_DEFINE(HAVE_TERMIO)
        PTY_TYPE=termios
        AC_MSG_RESULT(yes)
,
        AC_MSG_RESULT(no)
,
	AC_MSG_ERROR([Expect can't be cross compiled])
)

  # now check for the new style ttys (not yet posix)
  AC_MSG_CHECKING([for struct termios])
  AC_TRY_RUN([#include <termios.h>
  main()
  {
    struct termios tmp;
    exit(0);
  }],
        AC_DEFINE(HAVE_TERMIOS)
        PTY_TYPE=termios
        AC_MSG_RESULT(yes)
  ,
        AC_MSG_RESULT(no)
  ,
	AC_MSG_ERROR([Expect can't be cross compiled])
  )
fi

AC_MSG_CHECKING([if TCGETS or TCGETA in termios.h])
AC_TRY_RUN([
#include <termios.h>
main() {
#if defined(TCGETS) || defined(TCGETA)
	return 0;
#else
	return 1;
#endif
}],
	AC_DEFINE(HAVE_TCGETS_OR_TCGETA_IN_TERMIOS_H)
	AC_MSG_RESULT(yes)
,
	AC_MSG_RESULT(no)
,
	AC_MSG_ERROR([Expect can't be cross compiled])
)

AC_MSG_CHECKING([if TIOCGWINSZ in termios.h])
AC_TRY_RUN([
#include <termios.h>
main() {
#ifdef TIOCGWINSZ
	return 0;
#else
	return 1;
#endif
}],
	AC_DEFINE(HAVE_TIOCGWINSZ_IN_TERMIOS_H)
	AC_MSG_RESULT(yes)
,
	AC_MSG_RESULT(no)
,
	AC_MSG_ERROR([Expect can't be cross compiled])
)

# finally check for Cray style ttys
AC_MSG_CHECKING([for Cray-style ptys])
SETUID=":"
AC_TRY_RUN([
main(){
#ifdef CRAY
	return 0;
#else
	return 1;
#endif
}
],
	PTY_TYPE=unicos
	SETUID="chmod u+s"
	AC_MSG_RESULT(yes)
,
	AC_MSG_RESULT(no)
,
	AC_MSG_ERROR([Expect can't be cross compiled])
)

#
# Check for select and/or poll. If both exist, we prefer select.
# if neither exists, define SIMPLE_EVENT.
#
select=0
poll=0
unset ac_cv_func_select
AC_CHECK_FUNC(select, select=1)
AC_CHECK_FUNC(poll,   poll=1)
AC_MSG_CHECKING([whether to use select, poll, or simple])
if test $select -eq 1 ; then
  EVENT_TYPE=select
  AC_MSG_RESULT(select)
elif test $poll -eq 1 ; then
  EVENT_TYPE=poll
  AC_MSG_RESULT(poll)
else
  EVENT_TYPE=simple
  AC_MSG_RESULT(simple)
  AC_DEFINE(SIMPLE_EVENT)
fi

AC_HAVE_FUNCS(_getpty)
AC_HAVE_FUNCS(getpty)

#
# check for timezones
#
AC_MSG_CHECKING([for SV-style timezone])
AC_TRY_RUN([
extern char *tzname[2];
extern int daylight;
main()
{
  int *x = &daylight;
  char **y = tzname;

  exit(0);
}],
	AC_DEFINE(HAVE_SV_TIMEZONE)
	AC_MSG_RESULT(yes),
	AC_MSG_RESULT(no)
,
	AC_MSG_ERROR([Expect can't be cross compiled])
)

if test x"$no_x" != x"true" && test x"$no_tk" != x"true" ; then
  XPROGS="expectk \$(LIBEXPECTK)"
  if test x"${x_libraries}" != x -a x"${x_libraries}" != xNONE ; then
    XLIBPATH="-L${x_libraries}"
  else
    XLIBPATH=""
  fi

  if test x"${x_includes}" != x -a x"${x_includes}" != xNONE ; then
    XINCLUDES="-I$x_includes"
  else
    XINCLUDES=""
  fi
  
  XLIBS="-lX11"
else
  XPROGS="# no X support on this system"
  XINCLUDES=""
  XLIBPATH=""
  XLIBS=""
  AC_MSG_WARN([No X based programs will be built])
fi

#
# Set up makefile substitutions
#
AC_SUBST(CC)
AC_SUBST(EXP_CFLAGS)
AC_SUBST(XINCLUDES)
AC_SUBST(XLIBS)
AC_SUBST(XLIBPATH)
AC_SUBST(XPROGS)
AC_SUBST(PTY_TYPE)
AC_SUBST(EVENT_TYPE)
AC_SUBST(SETUID)
AC_SUBST(DEFAULT_STTY_ARGS)
AC_OUTPUT(Makefile)
